###############################################
# Input variable definition
###############################################
[INPUT_VARIABLES]
s_to_b_req0;
s_to_b_req1;
r_to_b_ack0;
r_to_b_ack1;
full;
empty;

###############################################
# Output variable definition
###############################################
[OUTPUT_VARIABLES]
b_to_s_ack0;
b_to_s_ack1;
b_to_r_req0;
b_to_r_req1;
stateG7_0;
stateG7_1;
enq;
deq;
stateG12;
slc0;

###############################################
# Environment specification
###############################################
[ENV_INITIAL]
(!s_to_b_req0);
(!s_to_b_req1);
(!r_to_b_ack0);
(!r_to_b_ack1);
full=0;
empty;

[ENV_TRANSITIONS]
G((s_to_b_req0 && (!b_to_s_ack0)) -> X(s_to_b_req0))
#A1
G(b_to_s_ack0 -> X((!s_to_b_req0)))
#A1
G((s_to_b_req1 && (!b_to_s_ack1)) -> X(s_to_b_req1))
#A1
G(b_to_s_ack1 -> X((!s_to_b_req1)))
#A1
G((!b_to_r_req0) -> X(!r_to_b_ack0))
#A3
G((b_to_r_req0 && r_to_b_ack0) -> X(r_to_b_ack0))
#A4
G((!b_to_r_req1) -> X(!r_to_b_ack1))
#A3
G((b_to_r_req1 && r_to_b_ack1) -> X(r_to_b_ack1))
#A4
G((enq && (!deq)) -> X((!empty)))
#A4
G((deq && (!enq)) -> X((!full)))
#A4
G((enq <-> deq) -> ((full <-> X(full)) && 
                        (empty <-> X(empty))))
#A4

[ENV_FAIRNESS]
G(F(b_to_r_req0 <-> r_to_b_ack0))
#A2
G(F(b_to_r_req1 <-> r_to_b_ack1))
#A2

###############################################
# System specification
###############################################
[SYS_INITIAL]
(!b_to_s_ack0);
(!b_to_s_ack1);
(!b_to_r_req0);
(!b_to_r_req1);
stateG7_0=0;
stateG7_1=1;
enq=0;
deq=0;
stateG12=0;
slc0=0;

[SYS_TRANSITIONS]
G(((!s_to_b_req0) && X(s_to_b_req0)) -> X(!b_to_s_ack0))
#G2
G(((!b_to_s_ack0) && (!s_to_b_req0)) -> X(!b_to_s_ack0))
#G2
G((b_to_s_ack0 && s_to_b_req0) -> X(b_to_s_ack0))
#G4
G(((!b_to_s_ack0)) || ((!b_to_s_ack1)))
#G5
G(((!s_to_b_req1) && X(s_to_b_req1)) -> X(!b_to_s_ack1))
#G2
G(((!b_to_s_ack1) && (!s_to_b_req1)) -> X(!b_to_s_ack1))
#G2
G((b_to_s_ack1 && s_to_b_req1) -> X(b_to_s_ack1))
#G4
G((b_to_r_req0 && (!r_to_b_ack0)) -> X(b_to_r_req0))
#G6
G(((!b_to_r_req0)) || ((!b_to_r_req1)))
#G7
G(r_to_b_ack0 -> X((!b_to_r_req0)))
#G8
G((b_to_r_req1 && (!r_to_b_ack1)) -> X(b_to_r_req1))
#G6
G(r_to_b_ack1 -> X((!b_to_r_req1)))
#G8
G((b_to_r_req0=1 * b_to_r_req1=1) -> FALSE)
#G7
G((stateG7_1=0 * b_to_r_req0=0 * b_to_r_req1=1) -> X(stateG7_1=1 * stateG7_0=0));#G7
G((stateG7_1=1 * b_to_r_req0=1 * b_to_r_req1=0) -> X(stateG7_1=0 * stateG7_0=0));#G7
G((stateG7_1=0 * b_to_r_req0=0 * b_to_r_req1=0) -> X(stateG7_1=0 * stateG7_0=1));#G7
G((stateG7_1=1 * b_to_r_req0=0 * b_to_r_req1=0) -> X(stateG7_1=1 * stateG7_0=1));#G7
G((stateG7_1=0 * stateG7_0=0 * b_to_r_req0=1 * b_to_r_req1=0) ->
 X(stateG7_1=0 * stateG7_0=0))
#G7
G((stateG7_1=1 * stateG7_0=0 * b_to_r_req0=0 * b_to_r_req1=1) ->
 X(stateG7_1=1 * stateG7_0=0))
#G7
G((stateG7_1=0 * stateG7_0=1 * b_to_r_req0=1) -> FALSE)
#G7
G((stateG7_1=1 * stateG7_0=1 * b_to_r_req1=1) -> FALSE)
#G7
G(((!b_to_s_ack0) && X(b_to_s_ack0)) -> X(enq))
#G9
G(((!b_to_s_ack0) && X(b_to_s_ack0))  -> X(!slc0))
#G9
G(((!b_to_s_ack1) && X(b_to_s_ack1)) -> X(enq))
#G9
G(((!b_to_s_ack1) && X(b_to_s_ack1)) <-> X(slc0))
#G9
G(((b_to_s_ack0 || X((!b_to_s_ack0))) && 
   (b_to_s_ack1 || X((!b_to_s_ack1)))) -> X(!enq))
#G9
G((r_to_b_ack0 && X((!r_to_b_ack0))) -> X(deq))
#G10
G((r_to_b_ack1 && X((!r_to_b_ack1))) -> X(deq))
#G10
#G10
G((((!r_to_b_ack0) || X(r_to_b_ack0)) && 
   ((!r_to_b_ack1) || X(r_to_b_ack1))) -> X((!deq)))
#G11
G((full && (!deq)) -> (!enq))
#G11
G(empty -> (!deq))
G(((!stateG12) && empty) -> X((!stateG12)))
#G12
G(((!stateG12) && deq  ) -> X((!stateG12)))
#G12
G(((!stateG12) && (!empty) && (!deq)) -> X(stateG12))
#G12
G((stateG12 && (!deq)  ) -> X(stateG12))
#G12
G((stateG12 && deq  ) -> X((!stateG12)))
#G12

[SYS_FAIRNESS]
G(F(s_to_b_req0 <-> b_to_s_ack0))
#G1+G2
G(F(s_to_b_req1 <-> b_to_s_ack1))
#G1+G2
G(F((!stateG12)))
#G12

